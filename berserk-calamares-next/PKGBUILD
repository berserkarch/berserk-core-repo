# Maintainer : Gaurav Raj <gaurav@thehackersbrain.dev>
# Calamares installer (fork) configured for Berserk Arch

pkgname=berserk-calamares-next
_pkgname=calamares
pkgver=26.01.24.1
pkgrel=1
release_name="rolling"
pkgdesc="Calamares installer for Berserk Arch"
arch=('any')
url="https://github.com/berserkarch/calamares"
license=('GPL3')
makedepends=('git' 'cmake' 'extra-cmake-modules' 'python-jsonschema' 'python-pyaml' 'python-unidecode' 'gawk')
conflicts=('calamares-git')
depends=('qt6-svg' 'qt6-webengine' 'yaml-cpp' 'networkmanager' 'upower' 'kcoreaddons' 'kconfig' 'ki18n' 'kservice'
  'kwidgetsaddons' 'kpmcore' 'squashfs-tools' 'rsync' 'pybind11' 'cryptsetup' 'doxygen' 'dmidecode'
  'gptfdisk' 'hwinfo' 'kparts' 'polkit-qt6' 'python' 'solid' 'qt6-tools' 'libpwquality' 'ckbcomp' 'qt6-declarative' 'berserk-config-calamares-next')
provides=("calamares")
options=(!strip !emptydirs)
source=("https://github.com/thehackersbrain/${_pkgname}/archive/refs/tags/${pkgver}.tar.gz")

sha256sums=('8d9f5bec2399c90237c5bb2bbd552001d98c30d90c59f9d8fdb90cbba630a81d')

prepare() {
  # Update branding.desc with the proper values
  replace_command='
    {
        gsub(/\${version}/,version);
        gsub(/\${release_name}/,release);
        print
    }
    '
  awk -i inplace -v version="${pkgver}" -v release="${release_name}" "$replace_command" "${srcdir}/calamares-${pkgver}/data/berserkarch/branding/berserkarch/branding.desc"
}

build() {
  cmake -B build -S "${srcdir}/calamares-${pkgver}" \
    -DWITH_QT6=ON \
    -DCMAKE_BUILD_TYPE=Debug \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DINSTALL_CONFIG=OFF \
    -DSKIP_MODULES="dracut \
    dummycpp dummyprocess dummypython dummypythonqt \
    finishedq initcpio keyboardq license localeq notesqml oemid \
    openrcdmcryptcfg plymouthcfg plasmalnf services-openrc \
    summaryq tracking webview welcomeq"
  export DESTDIR="$srcdir/build"
  make -C build
}

package() {
  make -C build DESTDIR="${pkgdir}" install
  # install -dm 755 "${pkgdir}/etc"
  # cp -rp "${srcdir}/calamares-${pkgver}/data/berserkarch" "${pkgdir}/etc/calamares"
}
