#https://github.com/calamares/calamares/releases

pkgname=berserk-calamares
_pkgname=calamares
pkgver=3.4.0
pkgrel=1
pkgdesc='Distribution-independent installer framework - latest git version'
arch=('i686' 'x86_64')
url="https://gitlab.com/berserkarch/berserk-packages/berserk-core-src/-/blob/main/berserk-calamares-early/"
license=('LGPL')
conflicts=('calamares-next' 'calamares-git')
provides=('calamares')
depends=(
  'boost-libs'
  'ckbcomp'
  'cryptsetup'
  'doxygen'
  'efibootmgr'
  'gptfdisk'
  'gtk-update-icon-cache'
  'hwinfo'
  'icu'
  'kconfig'
  'kcoreaddons'
  'kcrash'
  'ki18n'
  'kparts'
  'kpmcore'
  'kservice'
  'kwidgetsaddons'
  'libpwquality'
  'mkinitcpio-openswap'
  'polkit-qt6'
  'rsync'
  'qt6-declarative'
  'solid'
  'squashfs-tools'
  'yaml-cpp'
)
makedepends=(
  'boost'
  'cmake'
  'extra-cmake-modules'
  'git'
  'fakeroot'
  'ninja'
  'python-jsonschema'
  'python-pyaml'
  'python-unidecode'
  'qt6-tools'
)
backup=('usr/share/calamares/modules/bootloader.conf'
  'usr/share/calamares/modules/displaymanager.conf'
  'usr/share/calamares/modules/initcpio.conf'
  'usr/share/calamares/modules/unpackfs.conf')

source=("calamares::https://codeberg.org/Calamares/calamares/releases/download/v3.4.0/calamares-${pkgver}.tar.gz"
  "49-nopasswd-calamares.rules")
sha256sums=('d48eaf946e19ee62a3c2b5c204bb1b2f6d3b16349f6fecf03eeb725fe2e32b06'
            '56d85ff6bf860b9559b8c9f997ad9b1002f3fccc782073760eca505e3bddd176')

prepare() {
  sed -i -e 's/"Install configuration files" OFF/"Install configuration files" ON/' "$srcdir/$_pkgname-$pkgver/CMakeLists.txt"
  sed -i -e "s/desired_size = 512 \* 1024 \* 1024  \# 512MiB/desired_size = 512 \* 1024 \* 1024 \* 16  \# 8589MiB/" "$srcdir/$_pkgname-$pkgver/src/modules/fstab/main.py"

  sed -i -e 's#Name=.*#Name=Install Berserk Arch#g' "$srcdir/${_pkgname}-$pkgver/calamares.desktop"
  sed -i -e 's#GenericName=.*#GenericName=System Installer#g' "$srcdir/${_pkgname}-$pkgver/calamares.desktop"
  sed -i -e 's#Icon=.*#Icon=berserk-arch#g' "$srcdir/${_pkgname}-$pkgver/calamares.desktop"
  sed -i -e 's#Comment=.*#Comment=Berserk Arch System Installer#g' "$srcdir/${_pkgname}-$pkgver/calamares.desktop"
}

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  cmake -S . -B build \
    -G Ninja \
    -DCMAKE_BUILD_RPATH_USE_ORIGIN=ON \
    -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DWITH_APPSTREAM=OFF \
    -DWITH_PYBIND11=OFF \
    -DWITH_QT6=ON \
    -DSKIP_MODULES="dracut \
			dracutlukscfg \
			dummycpp \
			dummyprocess \
			dummypython \
			dummypythonqt \
			initramfs \
			initramfscfg \
			interactiveterminal \
			keyboardq \
			license \
			localeq \
			oemid \
			packagechooserq \
			partitionq \
			services-openrc \
			summaryq \
			tracking \
			usersq \
			welcomeq"

  cmake --build build
}

package() {
  cd "$srcdir/$_pkgname-$pkgver/build"
  DESTDIR="$pkgdir" cmake --install .

  install -Dm644 "${srcdir}/49-nopasswd-calamares.rules" "$pkgdir/etc/polkit-1/rules.d/49-nopasswd-calamares.rules"
  chmod 750 "$pkgdir"/etc/polkit-1/rules.d
}
